let wnd = api.window();
wnd.create(600, 400);

let dragCrt = func() {
	if (wnd.isAlive() == 0) {
		print("Window closed!");
		wnd = none;
		return;
	}
	wnd.drag();
	timeout(dragCrt, 0);
};
dragCrt();

let device = api.device();
let swapChain = api.swapChain();
let commandQueue = api.commandQueue();

let worlyData = none;
let worlyRenderer = none;

let generalInit = func() {
	device.create();
	commandQueue.create(device);
	swapChain.create(device, wnd, commandQueue);
	print("General Init!");
};

let startUpdateUV = func() {
	let offset = [];
	offset.push(0);
	offset.push(0);

	let update = func() {
		if (wnd.isAlive() == 0) {
			return;
		}

		offset[0] = offset[0] + 0.0001;
		offset[1] = offset[1] + 0.0001;

		worlyRenderer.constBuff.copyData(offset);

		timeout(update, 0);
	};
	update();
};

let tryStartRender = func() {
	if (worlyData == none) {
		return;
	}
	if (worlyRenderer == none) {
		return;
	}

	try {
		worlyRenderer.startRender(wnd, swapChain, commandQueue, worlyData);
		startUpdateUV();
	}  catch(e) {
		print(e);
	}
};

let requestRenderer = func() {
	let req = require("displayWorly.txt");
	req(device, func(renderer) {
		worlyRenderer = renderer;
		print("Renderer acquired!");
		tryStartRender();
	});
};


let worlyExecute = func() {
	try {
		print("Texture Processing Begin!");
		
		worlyData.execute(device, commandQueue, func() {
			print("Texture Processing End!");
			tryStartRender();
		});

	} catch(e) {
		print(e);
	}
};

let worlyInit = func() {
	let worly = require("worlyCompute.txt");
	worly.prepare(device, 256, func(data) {
		worlyData = data;
		print("Worly Init!");

		worlyExecute();
	});
};

try {
	generalInit();
	worlyInit();
	requestRenderer();
} catch(e) {
	print(e);
}
