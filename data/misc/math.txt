let utils = require("utils.txt");

let vector2 = func(x, y) {
	let res = [];
	res.push(x);
	res.push(y);
	return res;
};

let vector3 = func(x, y, z) {
	let res = [];
	res.push(x);
	res.push(y);
	res.push(z);

	return res;
};

let vector = func(x, y, z, w) {
	let res = [];
	res.push(x);
	res.push(y);
	res.push(z);
	res.push(w);

	return res;
};

let multiply = func(scalar, vector) {
	let tmp = utils.map(vector, func(x) {
		return scalar * x;
	});

	return utils.toList(tmp);
};

let addVectors = func(a, b) {
	let tmp = utils.zip(a, b);
	tmp = utils.map(tmp, func(x) {
		return x.fst + x.snd;
	});
	return utils.toList(tmp);
};

let subtractVectors = func(a, b) {
	let minusB = multiply(-1, b);
	return addVectors(a, minusB);
};

let dot = func(a, b) {
	let tmp = utils.zip(a, b);
	return utils.leftFold(0, tmp, func(x, y) {
		return x + y.fst * y.snd;
	});
};

let vectorLength = func(x) {
	let sq = dot(x, x);
	return api.math.sqrt(sq);
};

let normalize = func(x) {
	let l = dot(x, x);
	if (l == 0) {
		return x;
	}

	let l = vectorLength(x);
	return multiply(1 / l, x);
};

let vectorProduct = func(a, b) {
	return vector(a[1]*b[2] - a[2]*b[1], a[2]*b[0] - a[0]*b[2], a[0]*b[1] - a[1]*b[0]);
};

let quaternion = func(a, b, c, d) {
	return vector(a, b, c, d);
};

let multiplyQuaternions = func(x, y) {
	let a = x[0] * y[0] - x[1] * y[1] - x[2] * y[2] - x[3] * y[3];
	let b = x[0] * y[1] + x[1] * y[0] + x[2] * y[3] - x[3] * y[2];
	let c = x[0] * y[2] - x[1] * y[3] + x[2] * y[0] + x[3] * y[1];
	let d = x[0] * y[3] + x[1] * y[2] - x[2] * y[1] + x[3] * y[0];

	return quaternion(a, b, c, d);
};

let conjugate = func(q) {
	return quaternion(q[0], -q[1], -q[2], -q[3]);
};

let rotateVector = func(x, axis, angle) {
	let xQ = quaternion(0, x[0], x[1], x[2]);

	axis = normalize(axis);
	let rad = api.math.PI * angle / 180;

	rad = rad / 2;

	let cos = api.math.cos(rad);
	let sin = api.math.sin(rad);

	let q = quaternion(cos, -sin * axis[0], -sin * axis[1], -sin * axis[2]);

	let res = multiplyQuaternions(conjugate(q), multiplyQuaternions(xQ, q));
	return vector3(res[1], res[2], res[3]);
};

export.vector2 = vector2;
export.vector3 = vector3;
export.vector = vector;
export.multiply = multiply;
export.addVectors = addVectors;
export.subtractVectors = subtractVectors;
export.dot = dot;
export.vectorLength = vectorLength;
export.normalize = normalize;
export.vectorProduct = vectorProduct;
export.quaternion = quaternion;
export.multiplyQuaternions = multiplyQuaternions;
export.conjugate = conjugate;
export.rotateVector = rotateVector;

export.sqrt = api.math.sqrt;
export.sin = api.math.sin;
export.cos = api.math.cos;
export.asin = api.math.asin;
export.acos = api.math.acos;
export.PI = api.math.PI;
